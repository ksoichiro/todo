import org.springframework.security.crypto.password.StandardPasswordEncoder

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.2.2.RELEASE'
        classpath 'org.springframework:springloaded:1.2.1.RELEASE'
        classpath 'org.springframework.boot:spring-boot-starter-security:1.2.2.RELEASE'
    }
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'spring-boot'
apply plugin: 'idea'

repositories {
    jcenter()
}

dependencies {
    compile 'org.springframework.boot:spring-boot-starter-web:1.2.2.RELEASE'
    compile 'org.springframework.boot:spring-boot-starter-thymeleaf:1.2.2.RELEASE'
    compile 'org.springframework.boot:spring-boot-starter-data-jpa:1.2.2.RELEASE'
    compile 'org.springframework.boot:spring-boot-starter-security:1.2.2.RELEASE'
    compile 'mysql:mysql-connector-java:5.1.34'
    compile 'org.hibernate:hibernate-validator:5.1.3.Final'
    compile 'org.apache.tomcat.embed:tomcat-embed-el:8.0.15'
    compile 'org.webjars:jquery:2.1.3'
    compile 'org.webjars:jquery-ui:1.11.3'
    compile 'org.webjars:jquery-ui-themes:1.11.3'
    testCompile 'org.springframework.boot:sprintg-boot:starter-test:1.2.2.RELEASE'
}

jar {
    baseName = 'todo'
    version = '0.0.1-SNAPSHOT'
}

idea {
    module {
        inheritOutputDirs = false
        outputDir = file("${buildDir}/classes/main")
    }
}

// e.g. ./gradlew encodePassword -Pargs="test"
task encodePassword << {
    StandardPasswordEncoder encoder = new StandardPasswordEncoder()
    String username = 'test'
    if (project.hasProperty('args')) {
        username = project.args.split('\\s+')[0]
    }
    println "Encoded password for user ${username}:"
    println encoder.encode(username)
}
